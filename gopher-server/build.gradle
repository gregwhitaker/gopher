import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage

plugins {
    id "idea"
    id "java"
    id "org.springframework.boot"           version "2.2.5.RELEASE"
    id "io.spring.dependency-management"    version "1.0.9.RELEASE"
    id "com.coditory.integration-test"      version "1.1.2"
    id "com.bmuschko.docker-remote-api"     version "3.4.4"
}

group = 'com.gopherlinks'
version = '0.1.0'
sourceCompatibility = 1.8

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'com.github.gregwhitaker:envopts:1.0.0'
    implementation 'com.github.ben-manes.caffeine:caffeine:2.8.1'

    testImplementation 'junit:junit:4.12'
}

// Spring Boot
task bootRunLocal {
    bootRun.configure {
        systemProperty 'spring.profiles.active', 'local'
    }
}
bootRunLocal.finalizedBy bootRun

// Docker
task buildImage(type: DockerBuildImage, dependsOn: build) {
    buildArgs = ['JAR_FILE': "${bootJar.getArchiveFileName().get()}"]
    dockerFile = file("${projectDir}/src/main/docker/Dockerfile")
    inputDir = file("${projectDir}")
    tags = ["com.gopherlinks/${projectDir.name}", "com.gopherlinks/${projectDir.name}:${version}"]
}
